# Maintainer: Georges Discry <georges@discry.be>

pkgname=pycharm-professional
pkgver=4.0.1
pkgrel=101
pkgdesc="Powerful Python and Django IDE. Professional version."
arch=('i686' 'x86_64')
options=('!strip')
url="http://www.jetbrains.com/pycharm/"
provides=('pycharm')
license=('custom')
depends=('java-runtime>=6'
         'python'
         'python-coverage'
         'ipython' 
         'python2'
         'python2-coverage'
         'ipython2'
         'bash')
backup=("opt/${pkgname}/bin/pycharm.vmoptions"
        "opt/${pkgname}/bin/pycharm64.vmoptions")
changelog=ChangeLog
source=("http://download.jetbrains.com/python/${pkgname}-${pkgver}.tar.gz"
        'pycharm-professional.desktop')
sha256sums=('8413e1b5044b69eb4141cd3270d706292a23fee4e47de435007f8ab8cd83d213'
            '9b49e9747da180dbce763b9fd97ae6fd0e610950da9d5d3683e6a9c015b49196')

prepare() {
  cd "${srcdir}/pycharm-${pkgver}"
  echo '-Dswing.aatext=true' >> bin/pycharm.vmoptions
  echo '-Dswing.aatext=true' >> bin/pycharm64.vmoptions
  if [[ $CARCH = 'i686' ]]; then
    rm -f bin/libyjpagent-linux64.so
    rm -f bin/fsnotifier64
    rm -rf lib/libpty/linux/x86_64
  fi
  if [[ $CARCH = 'x86_64' ]]; then
    rm -f bin/libyjpagent-linux.so
    rm -f bin/fsnotifier
    rm -rf lib/libpty/linux/x86
  fi
  rm -rf lib/libpty/macosx
  rm -rf lib/libpty/win
}

package() {
  cd "${srcdir}/pycharm-${pkgver}"
  install -d "${pkgdir}/opt/${pkgname}"
  cp -R * "${pkgdir}/opt/${pkgname}/"
  install -Dm644 bin/pycharm.png "${pkgdir}/usr/share/pixmaps/${pkgname}.png"
  install -Dm644 "${srcdir}/${pkgname}.desktop" "${pkgdir}/usr/share/applications/${pkgname}.desktop"
  install -Dm644 license/PyCharm_license.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
  install -d "${pkgdir}/usr/bin"
  ln -s /opt/${pkgname}/bin/pycharm.sh "${pkgdir}/usr/bin/${pkgname}"
}

# vim:set ts=2 sw=2 et:
